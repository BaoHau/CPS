/* ThingML Framework */

// Base datatypes
import "datatypes.thingml" from stl

// Import everything in core [where is core?]
// import "core/scheduler.thingml"
// import "core/timer.thingml"

/* PSM must be included */
import "psm.thingml"
import "psm_datatypes_sim.thingml"
import "pim_messages.thingml"
import "timer.thingml"

thing PIM includes GeneralMsg, TemperatureMsg, OnOffMsg {
	provided port get_sensor {
		receives temperature, display_thermometer,monitor_temperature
	}
	required port request_sensor {
		sends add_thermometer, set_polling_interval, get_thermometer
	}
	required port request_actuator{
		sends add_device, SwitchOn, SwitchOff
	}
	provided port human_input {
		receives add_thermometer, get_thermometer, add_device, SwitchOn, SwitchOff, set_temperature, set_temp_deviation, set_polling_interval
	}
	required port human_output {
		sends temperature, prompt, display_thermometer, set_temperature, set_temp_deviation, add_thermometer, add_device,SwitchOn, SwitchOff
	}
	
	property tmrature:Double = 20 // the requested temperature for the room
	property delta: Double = 1 // set default temperature deviation = 1
	property thermo_id:Integer // the single thermometer identifier
	property switch_id:Integer // the single on-off switch identifier
	property last_switch_state: Integer = -1
	property last_temp: Double
	
	function turnOn(did : Integer) do
		request_actuator!SwitchOn(did)
		human_output!SwitchOn(did)
		last_switch_state = 1
	end
	
	function turnOff(did : Integer) do
		request_actuator!SwitchOff(did)
		human_output!SwitchOff(did)
		last_switch_state = 0
	end
	
	statechart PIM_behavior init Build {
		composite state Build init AddThermo keeps history {
			state AddThermo {
				transition -> AddDevice
				event addt:human_input?add_thermometer
				action do
					thermo_id=addt.id
					request_sensor!add_thermometer(thermo_id,addt.txt)
					human_output!add_thermometer(thermo_id,addt.txt)
					human_output!prompt("Please add one switch device") // SIMULATION: prompting on console for the user to react properly				
				end
				transition -> AddThermo // Cover other messages
				event human_input?add_device
				action do
					human_output!prompt("Please add thermometer")
				end
				transition -> AddThermo // Cover other messages
				event human_input?SwitchOn
				action do
					human_output!prompt("Please add thermometer")
				end
				transition -> AddThermo // Cover other messages
				event human_input?SwitchOff
				action do
					human_output!prompt("Please add thermometer")
				end
				transition -> AddThermo // Cover other messages
				event human_input?set_temperature
				action do
					human_output!prompt("Please add thermometer")
				end
				transition -> AddThermo // Cover other messages
				event human_input?set_temp_deviation
				action do
					human_output!prompt("Please add thermometer")
				end
				transition -> AddThermo // Cover other messages
				event human_input?set_polling_interval
				action do
					human_output!prompt("Please add thermometer")
				end
				transition -> AddThermo
				event human_input?get_thermometer
				action do
					human_output!prompt("Please add thermometer")
				end
				// temperature is handled on Build level
			}
			state AddDevice {
				transition -> SetThermostat
				event adddev:human_input?add_device
				action do
					switch_id=adddev.did
					request_actuator!add_device(switch_id)
					human_output!add_device(switch_id)
					human_output!prompt("Please give thermostat temperature setting") // SIMULATION: prompting on console for the user to react properly
				end
				
				transition -> AddDevice
				event human_input?SwitchOn
				action do
					human_output!prompt("Please add device")
				end
				transition -> AddDevice
				event human_input?SwitchOff
				action do
					human_output!prompt("Please add device")
				end
				transition -> AddDevice
				event human_input?set_polling_interval
				action do
					human_output!prompt("Please add device")
				end
				transition -> AddDevice
				event human_input?add_thermometer
				action do
					human_output!prompt("Thermometer already added, add device")
				end
				transition -> AddDevice
				event human_input?set_temperature
				action do
					human_output!prompt("Please add device")
				end
				transition -> AddDevice
				event human_input?set_temp_deviation
				action do
					human_output!prompt("Please add device")
				end
				// temperature and set_temperature handled on Build level
			}
			state SetThermostat {
				// Here the only message we want is the set_temperature, but this is on one level up to move to Running
				transition -> SetThermostat
				event human_input?add_thermometer
				action do
					human_output!prompt("Please set temperature for Thermostat")
				end			
				transition -> SetThermostat
				event human_input?add_device
				action do
					human_output!prompt("Please set temperature for Thermostat")
				end			
				transition -> SetThermostat
				event human_input?SwitchOn
				action do
					human_output!prompt("Please set temperature for Thermostat")
				end			
				transition -> SetThermostat
				event human_input?SwitchOff
				action do
					human_output!prompt("Please set temperature for Thermostat")
				end			
				transition -> SetThermostat
				event human_input?set_polling_interval
				action do
					human_output!prompt("Please set temperature for Thermostat")
				end			
			}
			
			// Normal transition to the Running state
			transition -> Running
			event set_temp:human_input?set_temperature
			action do
				tmrature = set_temp.t
				human_output!prompt("Now entering thermostat. Please give temperature observations") // SIMULATION: prompting on console for the user to react properly
				human_output!set_temperature(set_temp.t)
				human_output!display_thermometer(1,"test",last_temp)
			end
			
			transition -> Running
			event set_deviation:human_input?set_temp_deviation
			action do
				delta = set_deviation.d
				human_output!prompt("Now entering thermostat. Please give temperature observations") // SIMULATION: prompting on console for the user to react properly
				human_output!set_temp_deviation(set_deviation.d)
				human_output!display_thermometer(1,"test",last_temp)
			end
			
			//Escape situations
			transition -> Build
			event get_sensor?temperature 
				// just discard, the thermostat is not running, yet (161103)
				
			transition -> Build
			event get_temp:human_input?get_thermometer
			action do
				request_sensor!get_thermometer(get_temp.id)
			end
			
			transition -> Build
			event disp_temp:get_sensor?display_thermometer
			action do
				human_output!display_thermometer(disp_temp.id, disp_temp.txt, disp_temp.t)
			end
			
			transition -> Build
			event monitor:get_sensor?monitor_temperature
			action do
				println("Current temperature: " + monitor.t)
				last_temp = monitor.t
				human_output!display_thermometer(monitor.id, monitor.txt, monitor.t)
			end
			
		} // end Build
		
		composite state Running init Thermostat keeps history {	
			state Thermostat{
				transition -> On
				event swon:human_input?SwitchOn
				action do
					turnOn(swon.did)
				end
				 
				transition -> Off
				event swoff:human_input?SwitchOff
				action do
					turnOff(swoff.did)
				end
				
				transition -> Thermostat
				event set_temp:human_input?set_temperature
				action do
					tmrature = set_temp.t
					human_output!set_temperature(set_temp.t)
					human_output!display_thermometer(1,"test",last_temp)
				end
				transition -> Thermostat
				event set_deviation:human_input?set_temp_deviation
				action do
					delta = set_deviation.d
					human_output!set_temp_deviation(set_deviation.d)
					human_output!display_thermometer(1,"test",last_temp)
				end
			} // end Thermostat
			
			state On {
				transition -> Off
				event swoff:human_input?SwitchOff
				action do
					turnOff(swoff.did)
				end 
				transition -> On
				event swon:human_input?SwitchOn
				action do
					turnOn(swon.did)
				end
				transition -> Thermostat
				event set_temp:human_input?set_temperature
				action do
					tmrature = set_temp.t
					human_output!set_temperature(set_temp.t)
					human_output!display_thermometer(1,"test",last_temp)
				end
				transition -> Thermostat
				event set_deviation:human_input?set_temp_deviation
				action do
					delta = set_deviation.d
					human_output!set_temp_deviation(set_deviation.d)
					human_output!display_thermometer(1,"test",last_temp)
				end
			}
			state Off {
				transition -> Off
				event swoff:human_input?SwitchOff
				action do
					turnOff(swoff.did)
				end 
				transition -> On
				event swon:human_input?SwitchOn
				action do
					turnOn(swon.did)
				end
				transition -> Thermostat
				event set_temp:human_input?set_temperature
				action do
					tmrature = set_temp.t
					human_output!set_temperature(set_temp.t)
					human_output!display_thermometer(1,"test",last_temp)
				end
				transition -> Thermostat
				event set_deviation:human_input?set_temp_deviation
				action do
					delta = set_deviation.d
					human_output!set_temp_deviation(set_deviation.d)
					human_output!display_thermometer(1,"test",last_temp)
				end	
			}
			
			transition -> Running
			event get_temp:human_input?get_thermometer
			action do
				request_sensor!get_thermometer(get_temp.id)
			end
			
			transition -> Running
			event disp_temp:get_sensor?display_thermometer
			action do
				human_output!display_thermometer(disp_temp.id, disp_temp.txt, disp_temp.t)
			end
			
			// Transitions of the composite state Running
			transition -> Running
			event pollint:human_input?set_polling_interval
			action do
				// just forward the polling interval instructions to the PSM
				request_sensor!set_polling_interval(pollint.intrvl)
			end
			transition -> Running
			event temp:get_sensor?temperature
				// just discard - this should only happen when in On or Off states
			
			// Messages that should not occur, but may occur
			transition -> Running
			event human_input?add_thermometer
			action do
				human_output!prompt("Adding thermometers has been done and then blocked")
			end	
			transition -> Running
			event human_input?add_device
			action do
				human_output!prompt("Adding devices has been done and then blocked")
			end	
			// Messages the cannot occur - since they are always handled
			transition -> Running
			event human_input?SwitchOn
			action do
				human_output!prompt("INTERNAL ERROR: Impossible SwitchOn at PIM.Running")
			end		
			transition -> Running
			event human_input?SwitchOff
			action do
				human_output!prompt("INTERNAL ERROR: Impossible SwitchOff at PIM.Running")
			end		
			transition -> Running
			event human_input?set_temperature
			action do
				human_output!prompt("INTERNAL ERROR: Impossible set_temperature at PIM.Running")
			end
			transition -> Running
			event human_input?set_temp_deviation
			action do
				human_output!prompt("INTERNAL ERROR: Impossible set_temp_deviation at PIM.Running")
			end
			transition -> Running
			event monitor:get_sensor?monitor_temperature
			action do
				println("Current temperature: " + monitor.t)
				last_temp = monitor.t
				human_output!display_thermometer(monitor.id, monitor.txt, monitor.t)
			end
		} // end Running
	} // end PIM_behavior
} // end PIM thing

